plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.0.20'
}

group = 'de.fuballer'
version = '0.11.4'

repositories {
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'protocollib-repo'
        url = 'https://repo.dmulloy2.net/repository/public/'
    }
    maven {
        name = 'enginehub-repo'
        url = 'https://maven.enginehub.org/repo'
    }
    mavenCentral()
}

tasks.register('fatJar', Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { zipTree(it) }
    }
    with jar
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT'
    compileOnly 'com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit:2.11.1'
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.3.0-SNAPSHOT'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.0.10'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:2.0.10'
    implementation 'org.reflections:reflections:0.10.2'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

kotlin {
    jvmToolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}